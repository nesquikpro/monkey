buildscript {
    repositories {
        mavenCentral()
        maven { url = 'http://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
    }
    dependencies {
        classpath files('forgegradle/ForgeGradle-2.1.2.jar')
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse' // 'idea'

version = mod_version
group = mod_group
archivesBaseName = mod_id

sourceCompatibility = targetCompatibility = '1.8'

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
    options.encoding = "UTF-8"
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    mappings = mappings_version

    String resolved_core_config = mod_mixin_configs.replace('${mod_id}', mod_id)

    clientRunArgs.addAll(
            '--tweakClass', 'org.spongepowered.asm.launch.MixinTweaker',
            '--mixin', resolved_core_config
    )

    serverRunArgs.addAll(
            '--tweakClass', 'org.spongepowered.asm.launch.MixinTweaker',
            '--mixin', resolved_core_config
    )
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven' }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }
    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'

    embed 'com.squareup.okhttp3:okhttp:3.14.9'
    embed 'com.squareup.okio:okio:1.17.5'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'mod_id': mod_id, 'mod_name': mod_name, 'version': project.version,
                'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main {
        refMap = mod_mixin_refmap.replace('${mod_id}', mod_id)
    }
}

jar {
    from(configurations.embed.collect {
        it.isDirectory() ? it : zipTree(it)
    }) {
        exclude 'LICENSE.txt'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/maven/**'
        exclude 'META-INF/*.RSA'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.kotlin_module'
    }

    manifest.attributes(
            'ForceLoadAsMod': 'true',
            'FMLCorePluginContainsFMLMod': 'true',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': '0',
            'MixinConfigs': mod_mixin_configs.replace('${mod_id}', mod_id)
    )
}